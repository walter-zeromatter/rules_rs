load("@aspect_tools_telemetry_report//:defs.bzl", "TELEMETRY")  # buildifier: disable=load
load("@bazel_lib//lib:repo_utils.bzl", "repo_utils")
load("@bazel_skylib//lib:paths.bzl", "paths")
load("@rs_rust_host_tools//:defs.bzl", "RS_HOST_CARGO_LABEL")
load("//rs/private:annotations.bzl", "annotation_for", "build_annotation_map", "well_known_annotation_snippet_paths")
load("//rs/private:cargo_credentials.bzl", "load_cargo_credentials")
load("//rs/private:cfg_parser.bzl", "cfg_matches_expr_for_cfg_attrs", "triple_to_cfg_attrs")
load("//rs/private:crate_git_repository.bzl", "crate_git_repository")
load("//rs/private:crate_repository.bzl", "crate_repository", "local_crate_repository")
load("//rs/private:downloader.bzl", "download_metadata_for_git_crates", "download_sparse_registry_configs", "new_downloader_state", "parse_git_url", "sharded_path", "start_crate_registry_downloads", "start_github_downloads")
load("//rs/private:git_repository.bzl", "git_repository")
load("//rs/private:lint_flags.bzl", "cargo_toml_lint_flags")
load("//rs/private:repository_utils.bzl", "render_select")
load("//rs/private:resolver.bzl", "resolve")
load("//rs/private:select_utils.bzl", "compute_select")
load("//rs/private:semver.bzl", "select_matching_version")
load("//rs/private:toml2json.bzl", "run_toml2json")

def _spoke_repo(hub_name, name, version):
    s = "%s__%s-%s" % (hub_name, name, version)
    if "+" in s:
        s = s.replace("+", "-")
    return s

def _external_repo_for_git_source(remote, commit):
    return remote.replace("/", "_").replace(":", "_").replace("@", "_") + "_" + commit

def _platform(triple, use_experimental_platforms):
    if use_experimental_platforms:
        return "@rules_rs//rs/experimental/platforms/config:" + triple
    return "@rules_rust//rust/platform:" + triple.replace("-musl", "-gnu").replace("-gnullvm", "-msvc")

def _select(items):
    return {k: sorted(v) for k, v in items.items()}

def _exclude_deps_from_features(features):
    return [f for f in features if not f.startswith("dep:")]

def _shared_and_per_platform(platform_items, use_experimental_platforms):
    if not platform_items:
        return [], {}

    by_platform = {}
    for triple, items in platform_items.items():
        platform = _platform(triple, use_experimental_platforms)
        existing = by_platform.get(platform)
        if existing == None:
            by_platform[platform] = set(items)
        else:
            existing.update(items)

    deps, per_platform = compute_select([], by_platform)
    return sorted(deps), per_platform

def _render_string_list(items):
    return ",\n            ".join(['"%s"' % item for item in sorted(items)])

def _render_ordered_string_list(items):
    """Like _render_string_list but preserves insertion order."""
    return ",\n        ".join(['"%s"' % item for item in items])

def _render_string_list_dict(items_by_key):
    rendered = []
    for key, items in sorted(items_by_key.items()):
        rendered.append('"%s": [%s]' % (key, ", ".join(['"%s"' % item for item in sorted(items)])))
    return ",\n            ".join(rendered)

def _cfg_match_info_for_target(target, platform_cfg_attrs, cfg_match_cache):
    match_info = cfg_match_cache.get(target)
    if match_info:
        return match_info

    match_info = cfg_matches_expr_for_cfg_attrs(target, platform_cfg_attrs)
    cfg_match_cache[target] = match_info
    return match_info

def _add_to_dict(d, k, v):
    existing = d.get(k, [])
    if not existing:
        d[k] = existing
    existing.append(v)

def _fq_crate(name, version):
    return name + "-" + version

_INTERNAL_RUSTC_PLACEHOLDER_CRATES = [
    "rustc-std-workspace-alloc",
    "rustc-std-workspace-core",
    "rustc-std-workspace-std",
]

def _is_internal_rustc_placeholder(crate_name):
    return crate_name in _INTERNAL_RUSTC_PLACEHOLDER_CRATES

def _new_feature_resolutions(package_index, possible_deps, possible_features, platform_triples):
    return struct(
        features_enabled = {triple: set() for triple in platform_triples},
        build_deps = {triple: set() for triple in platform_triples},
        deps = {triple: set() for triple in platform_triples},
        aliases = {},
        package_index = package_index,

        # Following data is immutable, it comes from crates.io + Cargo.lock
        possible_deps = possible_deps,
        possible_features = possible_features,
    )

def _date(ctx, label):
    return
    result = ctx.execute(["gdate", '+"%Y-%m-%d %H:%M:%S.%3N"'])
    print(label, result.stdout)

def _normalize_path(path):
    return path.replace("\\", "/")

def _relative_to_workspace(path, workspace_root):
    normalized_root = _normalize_path(workspace_root)
    normalized_path = _normalize_path(path)

    if not paths.is_absolute(normalized_path):
        normalized_path = _normalize_path(paths.normalize(paths.join(normalized_root, normalized_path)))

    root_parts = [p for p in normalized_root.split("/") if p]
    path_parts = [p for p in normalized_path.split("/") if p]

    common = 0
    max_common = min(len(root_parts), len(path_parts))
    for idx in range(max_common):
        if root_parts[idx] != path_parts[idx]:
            break
        common = idx + 1

    rel_parts = [".."] * (len(root_parts) - common) + path_parts[common:]
    return "/".join(rel_parts) if rel_parts else "."

def _label_directory(label):
    idx = label.name.rfind("/")
    if idx == -1:
        return label.package

    return paths.join(label.package, label.name[:idx])

def _spec_to_dep_dict_inner(dep, spec, is_build = False):
    if type(spec) == "string":
        dep = {"name": dep}
    else:
        dep = {
            "name": dep,
            "optional": spec.get("optional", False),
            "default_features": spec.get("default_features", spec.get("default-features", True)),
            "features": spec.get("features", []),
        }
        if "package" in spec:
            dep["package"] = spec["package"]

    if is_build:
        dep["kind"] = "build"

    return dep

def _spec_to_dep_dict(dep, spec, annotation, workspace_cargo_toml_json, is_build = False):
    if type(spec) == "dict" and spec.get("workspace") == True:
        workspace = workspace_cargo_toml_json.get("workspace")
        if not workspace and annotation.workspace_cargo_toml != "Cargo.toml":
            fail("""

ERROR: `crate.annotation` for `{name}` has a `workspace_cargo_toml` pointing to a Cargo.toml without a `workspace` section. Please correct it in your MODULE.bazel!
Make sure you point to the `Cargo.toml` of the workspace, not of `{name}`!”

""".format(name = annotation.crate))

        inherited = _spec_to_dep_dict_inner(
            dep,
            workspace["dependencies"][dep],
            is_build,
        )

        extra_features = spec.get("features")
        if extra_features:
            inherited["features"] = sorted(set(extra_features + inherited.get("features", [])))

        if spec.get("optional"):
            inherited["optional"] = True

        if spec.get("package"):
            inherited["package"] = spec["package"]

        return inherited
    return _spec_to_dep_dict_inner(dep, spec, is_build)

def _cargo_metadata_dep_to_dep_dict(dep):
    rename = dep.get("rename")
    converted = {
        "name": rename or dep["name"],
        "optional": dep.get("optional", False),
        "default_features": dep.get("uses_default_features", True),
        "features": list(dep.get("features", [])),
    }

    req = dep.get("req")
    if req:
        converted["req"] = req

    kind = dep.get("kind")
    if kind and kind != "normal":
        converted["kind"] = kind

    target = dep.get("target")
    if target:
        converted["target"] = target

    if rename:
        converted["package"] = dep["name"]

    return converted

def _prepare_possible_deps(dependencies, converter = None):
    possible_deps = []

    for dep in dependencies:
        if converter:
            dep = converter(dep)

        if dep.get("kind") == "dev":
            continue

        dep_package = dep.get("package") or dep["name"]
        if _is_internal_rustc_placeholder(dep_package):
            continue

        if dep.get("default_features", True):
            _add_to_dict(dep, "features", "default")

        possible_deps.append(dep)

    return possible_deps

def _generate_hub_and_spokes(
        mctx,
        hub_name,
        annotations,
        suggested_annotation_snippet_paths,
        cargo_path,
        cargo_lock_path,
        workspace_cargo_toml_json,
        all_packages,
        sparse_registry_configs,
        platform_triples,
        cargo_credentials,
        cargo_config,
        validate_lockfile,
        debug,
        use_experimental_platforms,
        dry_run = False):
    """Generates repositories for the transitive closure of the Cargo workspace.

    Args:
        mctx (module_ctx): The module context object.
        hub_name (string): name
        annotations (dict): Annotation tags to apply.
        suggested_annotation_snippet_paths (dict): Mapping crate -> snippet file path.
        cargo_path (path): Path to hermetic `cargo` binary.
        cargo_lock_path (path): Cargo.lock path
        workspace_cargo_toml_json (dict): Parsed workspace Cargo.toml
        all_packages: list[package]: from cargo lock parsing
        sparse_registry_configs: dict[source, sparse registry config]
        platform_triples (list[string]): Triples to resolve for
        cargo_credentials (dict): Mapping of registry to auth token.
        cargo_config (label): .cargo/config.toml file
        validate_lockfile (bool): If true, validate we have appropriate versions in Cargo.lock
        debug (bool): Enable debug logging
        dry_run (bool): Run all computations but do not create repos. Useful for benchmarking.
    """
    _date(mctx, "start")

    mctx.report_progress("Reading workspace metadata")
    result = mctx.execute(
        [cargo_path, "metadata", "--no-deps", "--format-version=1", "--quiet"],
        working_directory = str(mctx.path(cargo_lock_path).dirname),
    )
    if result.return_code != 0:
        fail(result.stdout + "\n" + result.stderr)
    cargo_metadata = json.decode(result.stdout)

    _date(mctx, "parsed cargo metadata")

    existing_facts = getattr(mctx, "facts", {}) or {}
    facts = {}

    workspace_root = _normalize_path(cargo_metadata["workspace_root"])
    workspace_root_prefix = workspace_root + "/"
    workspace_member_keys = {}
    for package in cargo_metadata["packages"]:
        workspace_member_keys[(package["name"], package["version"])] = True

    dep_paths_by_name = {}
    for package in cargo_metadata["packages"]:
        for dep in package.get("dependencies", []):
            dep_path = dep.get("path")
            if dep_path:
                dep_paths_by_name[dep["name"]] = _relative_to_workspace(dep_path, workspace_root)

    patch_paths_by_name = {}
    for registry_patches in workspace_cargo_toml_json.get("patch", {}).values():
        for name, spec in registry_patches.items():
            if type(spec) != "dict":
                continue

            patch_path = spec.get("path")
            if not patch_path:
                continue

            if patch_path.startswith("/"):
                normalized = _normalize_path(patch_path)
                if not normalized.startswith(workspace_root_prefix):
                    fail("Patch path for %s points outside the workspace: %s" % (name, patch_path))
                rel_patch_path = normalized.removeprefix(workspace_root_prefix)
            else:
                rel_patch_path = _normalize_path(paths.normalize(patch_path))

            patch_paths_by_name[name] = rel_patch_path

    workspace_members = []
    packages = []

    for package in all_packages:
        pkg = dict(package)

        if pkg.get("source"):
            packages.append(pkg)
            continue

        key = (pkg["name"], pkg["version"])
        if key in workspace_member_keys:
            workspace_members.append(pkg)
            continue

        rel_path = patch_paths_by_name.get(pkg["name"]) or dep_paths_by_name.get(pkg["name"])
        local_path = rel_path
        if rel_path and not rel_path.startswith("/"):
            local_path = paths.join(workspace_root, rel_path)

        if not local_path:
            fail("Found a path dependency on %s %s but could not determine its path from Cargo.toml. Please declare it in [patch] or as a path dependency." % (pkg["name"], pkg["version"]))

        pkg["source"] = "path+" + hub_name + "/" + rel_path
        pkg["local_path"] = local_path
        packages.append(pkg)

    platform_cfg_attrs = [triple_to_cfg_attrs(triple) for triple in platform_triples]
    platform_cfg_attrs_by_triple = {}
    for cfg_attr in platform_cfg_attrs:
        platform_cfg_attrs_by_triple[cfg_attr["_triple"]] = cfg_attr

    mctx.report_progress("Computing dependencies and features")

    feature_resolutions_by_fq_crate = dict()

    # TODO(zbarsky): Would be nice to resolve for _ALL_PLATFORMS instead of per-triple, but it's complicated.
    cfg_match_cache = {None: struct(matches = platform_triples, uses_feature_cfg = False)}

    versions_by_name = dict()
    for package_index in range(len(packages)):
        package = packages[package_index]
        name = package["name"]
        version = package["version"]
        source = package["source"]

        _add_to_dict(versions_by_name, name, version)

        if source.startswith("sparse+"):
            key = name + "_" + version
            fact = existing_facts.get(key)
            if fact:
                facts[key] = fact
                fact = json.decode(fact)
            else:
                package["download_token"].wait()

                # TODO(zbarsky): Should we also dedupe this parsing?
                metadatas = mctx.read(name + ".jsonl").strip().split("\n")
                for metadata in metadatas:
                    metadata = json.decode(metadata)
                    if metadata["vers"] != version:
                        continue

                    features = metadata["features"]

                    # Crates published with newer Cargo populate this field for `resolver = "2"`.
                    # It can express more nuanced feature dependencies and overrides the keys from legacy features, if present.
                    features.update(metadata.get("features2") or {})

                    dependencies = metadata["deps"]

                    for dep in dependencies:
                        if dep["default_features"]:
                            dep.pop("default_features")
                        if not dep["features"]:
                            dep.pop("features")
                        if not dep["target"]:
                            dep.pop("target")
                        if dep["kind"] == "normal":
                            dep.pop("kind")
                        if not dep["optional"]:
                            dep.pop("optional")

                    fact = dict(
                        features = features,
                        dependencies = dependencies,
                    )

                    # Nest a serialized JSON since max path depth is 5.
                    facts[key] = json.encode(fact)
        elif source.startswith("path+"):
            key = source + "_" + name
            fact = existing_facts.get(key)
            if fact:
                facts[key] = fact
                fact = json.decode(fact)
            else:
                annotation = annotation_for(annotations, name, package["version"])
                cargo_toml_json = run_toml2json(mctx, paths.join(package["local_path"], "Cargo.toml"))

                dependencies = [
                    _spec_to_dep_dict(dep, spec, annotation, {})
                    for dep, spec in cargo_toml_json.get("dependencies", {}).items()
                ] + [
                    _spec_to_dep_dict(dep, spec, annotation, {}, is_build = True)
                    for dep, spec in cargo_toml_json.get("build-dependencies", {}).items()
                ]

                for target, value in cargo_toml_json.get("target", {}).items():
                    for dep, spec in value.get("dependencies", {}).items():
                        converted = _spec_to_dep_dict(dep, spec, annotation, {})
                        converted["target"] = target
                        dependencies.append(converted)

                fact = dict(
                    features = cargo_toml_json.get("features", {}),
                    dependencies = dependencies,
                    strip_prefix = "",
                )

                facts[key] = json.encode(fact)
            package["strip_prefix"] = fact.get("strip_prefix", "")
        elif source.startswith("git+"):
            key = source + "_" + name
            fact = existing_facts.get(key)
            if fact:
                facts[key] = fact
                fact = json.decode(fact)
            else:
                annotation = annotation_for(annotations, name, package["version"])
                info = package.get("member_crate_cargo_toml_info")
                if info:
                    # TODO(zbarsky): These tokens got enqueues last, so this can bottleneck
                    # We can try a bit harder to interleave things if we care.
                    info.token.wait()
                    workspace_cargo_toml_json = package["workspace_cargo_toml_json"]
                    cargo_toml_json = run_toml2json(mctx, info.path)
                else:
                    cargo_toml_json = package["cargo_toml_json"]
                    workspace_cargo_toml_json = package.get("workspace_cargo_toml_json")
                strip_prefix = package.get("strip_prefix", "")

                dependencies = [
                    _spec_to_dep_dict(dep, spec, annotation, workspace_cargo_toml_json)
                    for dep, spec in cargo_toml_json.get("dependencies", {}).items()
                ] + [
                    _spec_to_dep_dict(dep, spec, annotation, workspace_cargo_toml_json, is_build = True)
                    for dep, spec in cargo_toml_json.get("build-dependencies", {}).items()
                ]

                for target, value in cargo_toml_json.get("target", {}).items():
                    for dep, spec in value.get("dependencies", {}).items():
                        converted = _spec_to_dep_dict(dep, spec, annotation, workspace_cargo_toml_json)
                        converted["target"] = target
                        dependencies.append(converted)

                if not dependencies and debug:
                    print(name, version, package["source"])

                fact = dict(
                    features = cargo_toml_json.get("features", {}),
                    dependencies = dependencies,
                    strip_prefix = strip_prefix,
                )

                # Nest a serialized JSON since max path depth is 5.
                facts[key] = json.encode(fact)

            package["strip_prefix"] = fact["strip_prefix"]
        else:
            fail("Unknown source %s for crate %s" % (source, name))

        possible_features = fact["features"]
        possible_deps = _prepare_possible_deps(fact["dependencies"])
        feature_resolutions = _new_feature_resolutions(package_index, possible_deps, possible_features, platform_triples)
        package["feature_resolutions"] = feature_resolutions
        feature_resolutions_by_fq_crate[_fq_crate(name, version)] = feature_resolutions

    # Keep a resolver-only view that can include workspace members, unlike `versions_by_name`
    # which is used for spoke/hub emission.
    resolver_versions_by_name = {name: versions[:] for name, versions in versions_by_name.items()}

    workspace_members_by_key = {(package["name"], package["version"]): package for package in workspace_members}
    resolver_packages = packages[:]
    for package in cargo_metadata["packages"]:
        name = package["name"]
        version = package["version"]

        versions = resolver_versions_by_name.get(name, [])
        if version not in versions:
            if versions:
                versions.append(version)
            else:
                resolver_versions_by_name[name] = [version]

        possible_features = package.get("features", {})
        possible_deps = _prepare_possible_deps(
            package.get("dependencies", []),
            converter = _cargo_metadata_dep_to_dep_dict,
        )

        package_index = len(resolver_packages)
        lockfile_pkg = workspace_members_by_key.get((name, version), {})
        resolver_package = {
            "name": name,
            "version": version,
            "dependencies": lockfile_pkg.get("dependencies", []),
        }

        feature_resolutions = _new_feature_resolutions(package_index, possible_deps, possible_features, platform_triples)
        resolver_package["feature_resolutions"] = feature_resolutions
        feature_resolutions_by_fq_crate[_fq_crate(name, version)] = feature_resolutions

        resolver_packages.append(resolver_package)

    for package in resolver_packages:
        name = package["name"]
        deps_by_name = {}
        for maybe_fq_dep in package.get("dependencies", []):
            idx = maybe_fq_dep.find(" ")
            if idx != -1:
                dep = maybe_fq_dep[:idx]
                resolved_version = maybe_fq_dep[idx + 1:]
                _add_to_dict(deps_by_name, dep, resolved_version)

        for dep in package["feature_resolutions"].possible_deps:
            dep_package = dep.get("package")
            if not dep_package:
                dep_package = dep["name"]

            versions = resolver_versions_by_name.get(dep_package)
            if not versions:
                continue
            constrained_versions = deps_by_name.get(dep_package)
            if constrained_versions:
                versions = constrained_versions

            if len(versions) == 1:
                resolved_version = versions[0]
            else:
                req = dep.get("req")
                if not req:
                    continue

                resolved_version = select_matching_version(req, versions)
                if not resolved_version:
                    if not dep.get("optional"):
                        print("WARNING: %s: could not resolve %s %s among %s" % (name, dep_package, req, versions))
                    continue

            dep_fq = _fq_crate(dep_package, resolved_version)

            # Skip setting @crates// target for workspace members — they are
            # built from source at their workspace path, not vendored into the
            # hub.  Annotations (crate.annotation deps=[...]) or the workspace
            # path-dep resolution at line ~995 will supply the correct target.
            if (dep_package, resolved_version) not in workspace_member_keys:
                dep["bazel_target"] = "@%s//:%s" % (hub_name, dep_fq)

            dep["feature_resolutions"] = feature_resolutions_by_fq_crate[dep_fq]

            target = dep.get("target")
            match_info = _cfg_match_info_for_target(target, platform_cfg_attrs, cfg_match_cache)
            if match_info.uses_feature_cfg:
                dep["target_expr"] = target
                dep["feature_sensitive"] = True
                dep["target"] = set(platform_triples)
            else:
                dep["target"] = set(match_info.matches)

    _date(mctx, "set up resolutions")

    workspace_fq_deps = _compute_workspace_fq_deps(workspace_members, resolver_versions_by_name)

    workspace_dep_versions_by_name = {}
    workspace_dep_labels_by_triple = {triple: set() for triple in platform_triples}

    # Only files in the current Bazel workspace can/should be watched, so check where our manifests are located.
    watch_manifests = cargo_lock_path.repo_name == ""

    # Set initial set of features from Cargo.tomls
    for package in cargo_metadata["packages"]:
        if watch_manifests:
            mctx.watch(package["manifest_path"])

        fq_deps = workspace_fq_deps[package["name"]]

        for dep in package["dependencies"]:
            source = dep["source"]
            dep_name = dep["name"]
            dep_fq = fq_deps.get(dep_name)
            dep_version = None
            if dep_fq:
                dep_version = dep_fq[len(dep_name) + 1:]
            is_first_party_dep = not source and dep_version and (dep_name, dep_version) in workspace_member_keys

            if validate_lockfile and source and source.startswith("registry+"):
                req = dep["req"]
                fq = dep_fq
                if req and fq:
                    locked_version = fq[len(dep_name) + 1:]
                    if not select_matching_version(req, [locked_version]):
                        fail(("ERROR: Cargo.lock out of sync: %s requires %s %s but Cargo.lock has %s.\n\n" +
                              "If this is incorrect, please set `validate_lockfile = False` in `crate.from_cargo`\n" +
                              "and file a bug at https://github.com/hermeticbuild/rules_rs/issues/new") % (
                            package["name"],
                            dep_name,
                            req,
                            locked_version,
                        ))

            features = dep["features"]
            if dep["uses_default_features"]:
                features.append("default")

            if not dep_fq:
                continue

            if not is_first_party_dep:
                dep["bazel_target"] = "@%s//:%s" % (hub_name, dep_fq)

            feature_resolutions = feature_resolutions_by_fq_crate[dep_fq]

            if not is_first_party_dep:
                versions = workspace_dep_versions_by_name.get(dep_name)
                if not versions:
                    versions = set()
                    workspace_dep_versions_by_name[dep_name] = versions
                versions.add(dep_fq)

            target = dep.get("target")
            match_info = _cfg_match_info_for_target(target, platform_cfg_attrs, cfg_match_cache)

            for triple in match_info.matches:
                if not is_first_party_dep:
                    workspace_dep_labels_by_triple[triple].add(":" + dep_name)
                feature_resolutions.features_enabled[triple].update(features)

    # Set initial set of features from annotations
    for crate, annotation_versions in annotations.items():
        for version_key, annotation in annotation_versions.items():
            target_versions = resolver_versions_by_name.get(crate, [])
            if version_key != "*":
                if version_key not in target_versions:
                    continue
                target_versions = [version_key]
            if not annotation.crate_features and not annotation.crate_features_select:
                continue
            for version in target_versions:
                features_enabled = feature_resolutions_by_fq_crate[_fq_crate(crate, version)].features_enabled
                if annotation.crate_features:
                    for triple in platform_triples:
                        features_enabled[triple].update(annotation.crate_features)
                for triple, features in annotation.crate_features_select.items():
                    if triple in features_enabled:
                        features_enabled[triple].update(features)

    _date(mctx, "set up initial deps!")

    resolve(mctx, resolver_packages, feature_resolutions_by_fq_crate, platform_cfg_attrs_by_triple, debug)

    # Validate that we aren't trying to enable any `dep:foo` features that were not even in the lockfile.
    for package in packages:
        feature_resolutions = package["feature_resolutions"]
        features_enabled = feature_resolutions.features_enabled

        for dep in feature_resolutions.possible_deps:
            if "bazel_target" in dep:
                continue

            prefixed_dep_alias = "dep:" + dep["name"]

            for triple in platform_triples:
                if prefixed_dep_alias in features_enabled[triple]:
                    fail("Crate %s has enabled %s but it was not in the lockfile..." % (package["name"], prefixed_dep_alias))

    mctx.report_progress("Initializing spokes")

    use_home_cargo_credentials = bool(cargo_credentials)

    for package in packages:
        crate_name = package["name"]
        version = package["version"]
        source = package["source"]

        feature_resolutions = feature_resolutions_by_fq_crate[_fq_crate(crate_name, version)]

        annotation = annotation_for(annotations, crate_name, version)
        suggested_annotation = None
        if annotation.gen_build_script == "auto":
            snippet_path = suggested_annotation_snippet_paths.get(crate_name)
            if snippet_path:
                suggested_annotation = mctx.read(snippet_path).strip()

        if suggested_annotation:
            print("""
WARNING: A well-known crate annotation exists for {crate}! Apply the following to your MODULE.bazel:

```
{formatted_well_known_annotation}
```

You can disable this warning by configuring your MODULE.bazel like so:

```
crate.annotation(
    crate = "{crate}",
    gen_build_script = "on",
)
```""".format(
                crate = crate_name,
                formatted_well_known_annotation = suggested_annotation,
            ))

        kwargs = dict(
            hub_name = hub_name,
            additive_build_file = annotation.additive_build_file,
            additive_build_file_content = annotation.additive_build_file_content,
            gen_build_script = annotation.gen_build_script,
            build_script_deps = [],
            build_script_deps_select = _select(feature_resolutions.build_deps),
            build_script_data = annotation.build_script_data,
            build_script_data_select = annotation.build_script_data_select,
            build_script_env = annotation.build_script_env,
            build_script_toolchains = annotation.build_script_toolchains,
            build_script_tools = annotation.build_script_tools,
            build_script_tags = annotation.build_script_tags,
            build_script_tools_select = annotation.build_script_tools_select,
            build_script_env_select = annotation.build_script_env_select,
            rustc_flags = annotation.rustc_flags,
            rustc_flags_select = annotation.rustc_flags_select,
            data = annotation.data,
            deps = annotation.deps,
            crate_tags = annotation.tags,
            deps_select = _select(feature_resolutions.deps),
            aliases = feature_resolutions.aliases,
            gen_binaries = annotation.gen_binaries,
            crate_features = annotation.crate_features,
            crate_features_select = _select(feature_resolutions.features_enabled),
            patch_args = annotation.patch_args,
            patch_tool = annotation.patch_tool,
            patches = annotation.patches,
            use_experimental_platforms = use_experimental_platforms,
        )

        repo_name = _spoke_repo(hub_name, crate_name, version)

        if source.startswith("sparse+"):
            checksum = package["checksum"]
            url = sparse_registry_configs[source].format(**{
                "crate": crate_name,
                "version": version,
                "prefix": sharded_path(crate_name),
                "lowerprefix": sharded_path(crate_name.lower()),
                "sha256-checksum": checksum,
            })

            if dry_run:
                continue

            crate_repository(
                name = repo_name,
                url = url,
                strip_prefix = "%s-%s" % (crate_name, version),
                checksum = checksum,
                # The repository will need to recompute these, but this lets us avoid serializing them.
                use_home_cargo_credentials = use_home_cargo_credentials,
                cargo_config = cargo_config,
                source = source,
                **kwargs
            )
        elif source.startswith("path+"):
            if dry_run:
                continue

            local_crate_repository(
                name = repo_name,
                path = package["local_path"],
                **kwargs
            )
        elif source.startswith("git+"):
            remote, commit = parse_git_url(source)

            strip_prefix = package.get("strip_prefix")
            workspace_cargo_toml = annotation.workspace_cargo_toml
            if workspace_cargo_toml != "Cargo.toml":
                strip_prefix = workspace_cargo_toml.removesuffix("Cargo.toml") + (strip_prefix or "")

            if dry_run:
                continue

            crate_git_repository(
                name = repo_name,
                strip_prefix = strip_prefix,
                git_repo_label = "@" + _external_repo_for_git_source(remote, commit),
                workspace_cargo_toml = annotation.workspace_cargo_toml,
                **kwargs
            )
        else:
            fail("Unknown source %s for crate %s" % (source, crate_name))

    _date(mctx, "created repos")

    mctx.report_progress("Initializing hub")

    hub_contents = []
    for name, versions in versions_by_name.items():
        for version in versions:
            binaries = annotation_for(annotations, name, version).gen_binaries
            spoke_repo = _spoke_repo(hub_name, name, version)

            hub_contents.append("""
alias(
    name = "{name}-{version}",
    actual = "@{spoke_repo}//:{name}",
)""".format(name = name, version = version, spoke_repo = spoke_repo))

            for binary in binaries:
                hub_contents.append("""
alias(
    name = "{name}-{version}__{binary}",
    actual = "@{spoke_repo}//:{binary}__bin",
)""".format(name = name, version = version, binary = binary, spoke_repo = spoke_repo))

        workspace_versions = workspace_dep_versions_by_name.get(name)
        if workspace_versions:
            fq = sorted(workspace_versions)[-1]
            default_version = fq[len(name) + 1:]
            binaries = annotation_for(annotations, name, default_version).gen_binaries

            hub_contents.append("""
alias(
    name = "{name}",
    actual = ":{fq}",
)""".format(name = name, fq = fq))

            for binary in binaries:
                hub_contents.append("""
alias(
    name = "{name}__{binary}",
    actual = ":{fq}__{binary}",
)""".format(name = name, fq = fq, binary = binary))

    workspace_deps, conditional_workspace_deps = render_select(
        [],
        workspace_dep_labels_by_triple,
        use_experimental_platforms,
    )

    hub_contents.append(
        """
package(
    default_visibility = ["//visibility:public"],
)

filegroup(
    name = "_workspace_deps",
    srcs = [
        %s
    ]%s,
)""" % (
            ",\n        ".join(['"%s"' % dep for dep in sorted(workspace_deps)]),
            " + " + conditional_workspace_deps if conditional_workspace_deps else "",
        ),
    )

    lint_flags = cargo_toml_lint_flags(workspace_cargo_toml_json)
    hub_contents.append(
        """
load("@rules_rs//rs/private:cargo_lints.bzl", "cargo_lints")

cargo_lints(
    name = "cargo_lints",
    rustc_lint_flags = [
        {rustc}
    ],
    clippy_lint_flags = [
        {clippy}
    ],
    rustdoc_lint_flags = [
        {rustdoc}
    ],
)""".format(
            rustc = _render_ordered_string_list(lint_flags.rustc_lint_flags),
            clippy = _render_ordered_string_list(lint_flags.clippy_lint_flags),
            rustdoc = _render_ordered_string_list(lint_flags.rustdoc_lint_flags),
        ),
    )

    resolved_platforms = []
    for triple in platform_triples:
        platform = _platform(triple, use_experimental_platforms)
        if platform not in resolved_platforms:
            resolved_platforms.append(platform)

    defs_bzl_contents = \
        """load(":data.bzl", "DEP_DATA")
load("@rules_rs//rs/private:all_crate_deps.bzl", _all_crate_deps = "all_crate_deps")

_PLATFORMS = [
    {platforms}
]

def aliases(package_name = None):
    dep_data = DEP_DATA.get(package_name or native.package_name())
    if not dep_data:
        return {{}}

    return dep_data["aliases"]

def all_crate_deps(
        normal = False,
        normal_dev = False,
        build = False,
        package_name = None,
        cargo_only = False):

    dep_data = DEP_DATA.get(package_name or native.package_name())
    if not dep_data:
        return []

    return _all_crate_deps(
        dep_data,
        platforms = _PLATFORMS,
        normal = normal,
        normal_dev = normal_dev,
        build = build,
        filter_prefix = {this_repo} if cargo_only else None,
    )

RESOLVED_PLATFORMS = select({{
    {target_compatible_with},
    "//conditions:default": ["@platforms//:incompatible"],
}})
""".format(
            platforms = _render_string_list(resolved_platforms),
            target_compatible_with = ",\n    ".join(['"%s": []' % platform for platform in resolved_platforms]),
            this_repo = repr("@" + hub_name + "//:"),
        )

    _date(mctx, "done")

    repo_root = _normalize_path(cargo_metadata["workspace_root"])
    workspace_package = _label_directory(cargo_lock_path)

    workspace_dep_stanzas = []
    for package in cargo_metadata["packages"]:
        aliases = {}
        crate_features = {triple: set() for triple in platform_triples}
        deps = {triple: set() for triple in platform_triples}
        build_deps = {triple: set() for triple in platform_triples}
        dev_deps = {triple: set() for triple in platform_triples}
        package_dir = _normalize_path(package["manifest_path"]).removeprefix(repo_root + "/").removesuffix("/Cargo.toml")
        binaries = {}
        shared_libraries = {}
        feature_resolutions = feature_resolutions_by_fq_crate.get(_fq_crate(package["name"], package["version"]))

        for target in package.get("targets", []):
            kinds = target.get("kind", [])
            if "cdylib" not in kinds and "bin" not in kinds:
                continue

            src_path = target.get("src_path")
            if not src_path:
                continue

            entrypoint = _normalize_path(src_path).removeprefix(repo_root + "/")
            if package_dir and entrypoint.startswith(package_dir + "/"):
                entrypoint = entrypoint.removeprefix(package_dir + "/")

            if "cdylib" in kinds:
                shared_libraries[target["name"]] = entrypoint
            elif "bin" in kinds:
                binaries[target["name"]] = entrypoint

        for dep in package["dependencies"]:
            bazel_target = dep.get("bazel_target")
            if not bazel_target:
                bazel_target = "//" + paths.join(workspace_package, _normalize_path(dep["path"]).removeprefix(repo_root + "/"))

            if dep.get("rename"):
                aliases[bazel_target] = dep["rename"].replace("-", "_")
            elif dep.get("path"):
                aliases[bazel_target] = dep["name"].replace("-", "_")

            target = dep.get("target")
            match_info = _cfg_match_info_for_target(target, platform_cfg_attrs, cfg_match_cache)
            match = match_info.matches

            kind = dep["kind"]
            if kind == "dev":
                target_deps = dev_deps
            elif kind == "build":
                target_deps = build_deps
            else:
                target_deps = deps

            for triple in match:
                if dep.get("optional") and feature_resolutions:
                    dep_name = dep.get("rename") or dep["name"]
                    triple_features = feature_resolutions.features_enabled[triple]
                    if dep_name not in triple_features and ("dep:" + dep_name) not in triple_features:
                        continue

                target_deps[triple].add(bazel_target)

        if feature_resolutions:
            for triple in platform_triples:
                crate_features[triple].update(_exclude_deps_from_features(feature_resolutions.features_enabled[triple]))

        bazel_package = paths.join(workspace_package, package_dir)

        crate_features, crate_features_by_platform = _shared_and_per_platform(crate_features, use_experimental_platforms)
        deps, deps_by_platform = _shared_and_per_platform(deps, use_experimental_platforms)
        build_deps, build_deps_by_platform = _shared_and_per_platform(build_deps, use_experimental_platforms)
        dev_deps, dev_deps_by_platform = _shared_and_per_platform(dev_deps, use_experimental_platforms)

        workspace_dep_stanzas.append("""
    {bazel_package}: {{
        "aliases": {{
            {aliases}
        }},
        "crate_features": [
            {crate_features}
        ],
        "crate_features_by_platform": {{
            {crate_features_by_platform}
        }},
        "deps": [
            {deps}
        ],
        "deps_by_platform": {{
            {deps_by_platform}
        }},
        "build_deps": [
            {build_deps}
        ],
        "build_deps_by_platform": {{
            {build_deps_by_platform}
        }},
        "dev_deps": [
            {dev_deps}
        ],
        "dev_deps_by_platform": {{
            {dev_deps_by_platform}
        }},
        "binaries": {{
            {binaries}
        }},
        "shared_libraries": {{
            {shared_libraries}
        }},
    }},""".format(
            bazel_package = repr(bazel_package),
            aliases = ",\n            ".join(['"%s": "%s"' % kv for kv in sorted(aliases.items())]),
            crate_features = _render_string_list(crate_features),
            crate_features_by_platform = _render_string_list_dict(crate_features_by_platform),
            deps = _render_string_list(deps),
            deps_by_platform = _render_string_list_dict(deps_by_platform),
            build_deps = _render_string_list(build_deps),
            build_deps_by_platform = _render_string_list_dict(build_deps_by_platform),
            dev_deps = _render_string_list(dev_deps),
            dev_deps_by_platform = _render_string_list_dict(dev_deps_by_platform),
            binaries = ",\n            ".join(['"%s": "%s"' % kv for kv in sorted(binaries.items())]),
            shared_libraries = ",\n            ".join(['"%s": "%s"' % kv for kv in sorted(shared_libraries.items())]),
        ))

    data_bzl_contents = "DEP_DATA = {" + "\n".join(workspace_dep_stanzas) + "\n}"

    if dry_run:
        return

    _hub_repo(
        name = hub_name,
        contents = {
            "BUILD.bazel": "\n".join(hub_contents),
            "defs.bzl": defs_bzl_contents,
            "data.bzl": data_bzl_contents,
        },
    )

    return facts

def _compute_package_fq_deps(package, versions_by_name, strict = True):
    possible_dep_fq_crate_by_name = {}

    for maybe_fq_dep in package.get("dependencies", []):
        idx = maybe_fq_dep.find(" ")
        if idx == -1:
            # Only one version
            versions = versions_by_name.get(maybe_fq_dep)
            if not versions:
                if strict:
                    fail("Malformed lockfile?")
                continue
            dep = maybe_fq_dep
            resolved_version = versions[0]
        else:
            dep = maybe_fq_dep[:idx]
            resolved_version = maybe_fq_dep[idx + 1:]

        possible_dep_fq_crate_by_name[dep] = _fq_crate(dep, resolved_version)

    return possible_dep_fq_crate_by_name

def _compute_workspace_fq_deps(workspace_members, versions_by_name):
    workspace_fq_deps = {}

    for workspace_member in workspace_members:
        fq_deps = _compute_package_fq_deps(workspace_member, versions_by_name, strict = False)
        workspace_fq_deps[workspace_member["name"]] = fq_deps

    return workspace_fq_deps

def _crate_impl(mctx):
    # TODO(zbarsky): Kick off `cargo` fetch early to mitigate https://github.com/bazelbuild/bazel/issues/26995
    cargo_path = mctx.path(RS_HOST_CARGO_LABEL)

    # And toml2json
    toml2json = mctx.path(Label("@toml2json_%s//file:downloaded" % repo_utils.platform(mctx)))

    downloader_state = new_downloader_state()
    suggested_annotation_snippet_paths = well_known_annotation_snippet_paths(mctx)

    packages_by_hub_name = {}
    cargo_toml_by_hub_name = {}
    cargo_credentials_by_hub_name = {}
    annotations_by_hub_name = {}

    for mod in mctx.modules:
        if not mod.tags.from_cargo:
            fail("`.from_cargo` is required. Please update %s" % mod.name)

        for cfg in mod.tags.from_cargo:
            annotations = build_annotation_map(mod, cfg.name)
            annotations_by_hub_name[cfg.name] = annotations
            mctx.watch(cfg.cargo_lock)
            mctx.watch(cfg.cargo_toml)
            cargo_toml_by_hub_name[cfg.name] = run_toml2json(mctx, cfg.cargo_toml)
            cargo_lock = run_toml2json(mctx, cfg.cargo_lock)
            parsed_packages = cargo_lock.get("package", [])
            for package in parsed_packages:
                package["hub_name"] = cfg.name
            packages_by_hub_name[cfg.name] = parsed_packages

            # Process git downloads first because they may require a followup download if the repo is a workspace,
            # so we want to enqueue them early so they don't get delayed by 1-shot registry downloads.
            start_github_downloads(mctx, downloader_state, annotations, parsed_packages)

    for mod in mctx.modules:
        for cfg in mod.tags.from_cargo:
            annotations = build_annotation_map(mod, cfg.name)

            if cfg.use_home_cargo_credentials:
                if not cfg.cargo_config:
                    fail("Must provide cargo_config when using cargo credentials")

                cargo_credentials = load_cargo_credentials(mctx, cfg.cargo_config)
            else:
                cargo_credentials = {}

            cargo_credentials_by_hub_name[cfg.name] = cargo_credentials
            start_crate_registry_downloads(mctx, downloader_state, annotations, packages_by_hub_name[cfg.name], cargo_credentials, cfg.debug)

    for fetch_state in downloader_state.in_flight_git_crate_fetches_by_url.values():
        fetch_state.download_token.wait()

    download_metadata_for_git_crates(mctx, downloader_state, annotations_by_hub_name)

    # TODO(zbarsky): Unfortunate that we block on the download for crates.io even though it's well-known.
    # Should we hardcode it?
    sparse_registry_configs = download_sparse_registry_configs(mctx, downloader_state)

    facts = {}
    direct_deps = []
    direct_dev_deps = []

    for mod in mctx.modules:
        for cfg in mod.tags.from_cargo:
            if mod.is_root:
                if mctx.is_dev_dependency(cfg):
                    direct_dev_deps.append(cfg.name)
                else:
                    direct_deps.append(cfg.name)

            hub_packages = packages_by_hub_name[cfg.name]
            cargo_credentials = cargo_credentials_by_hub_name[cfg.name]

            annotations = build_annotation_map(mod, cfg.name)

            if cfg.debug:
                for _ in range(25):
                    _generate_hub_and_spokes(mctx, cfg.name, annotations, suggested_annotation_snippet_paths, cargo_path, cfg.cargo_lock, cargo_toml_by_hub_name[cfg.name], hub_packages, sparse_registry_configs, cfg.platform_triples, cargo_credentials, cfg.cargo_config, cfg.validate_lockfile, cfg.debug, cfg.use_experimental_platforms, dry_run = True)

            facts |= _generate_hub_and_spokes(mctx, cfg.name, annotations, suggested_annotation_snippet_paths, cargo_path, cfg.cargo_lock, cargo_toml_by_hub_name[cfg.name], hub_packages, sparse_registry_configs, cfg.platform_triples, cargo_credentials, cfg.cargo_config, cfg.validate_lockfile, cfg.debug, cfg.use_experimental_platforms)

    # Lay down the git repos we will need; per-crate git_repository can clone from these.
    git_sources = set()
    for mod in mctx.modules:
        for cfg in mod.tags.from_cargo:
            for package in packages_by_hub_name[cfg.name]:
                source = package.get("source", "")
                if source.startswith("git+"):
                    git_sources.add(source)

    for git_source in git_sources:
        remote, commit = parse_git_url(git_source)

        git_repository(
            name = _external_repo_for_git_source(remote, commit),
            commit = commit,
            remote = remote,
        )

    kwargs = dict(
        root_module_direct_deps = direct_deps,
        root_module_direct_dev_deps = direct_dev_deps,
        reproducible = True,
    )

    if hasattr(mctx, "facts"):
        kwargs["facts"] = facts

    return mctx.extension_metadata(**kwargs)

_from_cargo = tag_class(
    doc = "Generates a repo @crates from a Cargo.toml / Cargo.lock pair.",
    # Ordering is controlled for readability in generated docs.
    attrs = {
        "name": attr.string(
            doc = "The name of the repo to generate",
            default = "crates",
        ),
    } | {
        "cargo_toml": attr.label(
            doc = "The workspace-level Cargo.toml. There can be multiple crates in the workspace.",
        ),
        "cargo_lock": attr.label(),
        "cargo_config": attr.label(),
        "use_home_cargo_credentials": attr.bool(
            doc = "If set, the ruleset will load `~/cargo/credentials.toml` and attach those credentials to registry requests.",
        ),
        "platform_triples": attr.string_list(
            mandatory = True,
            doc = "The set of triples to resolve for. They must correspond to the union of any exec/target platforms that will participate in your build.",
        ),
        "use_experimental_platforms": attr.bool(
            doc = "If true, use experimental rules_rs platforms. If false, use the stable rules_rust platforms.",
            default = False,
        ),
        "validate_lockfile": attr.bool(
            doc = "If true, fail if Cargo.lock versions don't satisfy Cargo.toml requirements.",
            default = True,
        ),
        "debug": attr.bool(),
    },
)

_relative_label_list = attr.string_list

_annotation = tag_class(
    doc = "A collection of extra attributes and settings for a particular crate.",
    attrs = {
        "crate": attr.string(
            doc = "The name of the crate the annotation is applied to",
            mandatory = True,
        ),
        "version": attr.string(
            doc = "The version of the crate the annotation is applied to. Defaults to all versions.",
            default = "*",
        ),
        "repositories": attr.string_list(
            doc = "A list of repository names specified from `crate.from_cargo(name=...)` that this annotation is applied to. Defaults to all repositories.",
            default = [],
        ),
    } | {
        "additive_build_file": attr.label(
            doc = "A file containing extra contents to write to the bottom of generated BUILD files.",
        ),
        "additive_build_file_content": attr.string(
            doc = "Extra contents to write to the bottom of generated BUILD files.",
        ),
        # "alias_rule": attr.string(
        #     doc = "Alias rule to use instead of `native.alias()`.  Overrides [render_config](#render_config)'s 'default_alias_rule'.",
        # ),
        "build_script_data": _relative_label_list(
            doc = "A list of labels to add to a crate's `cargo_build_script::data` attribute.",
        ),
        # "build_script_data_glob": attr.string_list(
        #     doc = "A list of glob patterns to add to a crate's `cargo_build_script::data` attribute",
        # ),
        "build_script_data_select": attr.string_list_dict(
            doc = "A list of labels to add to a crate's `cargo_build_script::data` attribute. Keys should be the platform triplet. Value should be a list of labels.",
        ),
        # "build_script_deps": _relative_label_list(
        #     doc = "A list of labels to add to a crate's `cargo_build_script::deps` attribute.",
        # ),
        "build_script_env": attr.string_dict(
            doc = "Additional environment variables to set on a crate's `cargo_build_script::env` attribute.",
        ),
        "build_script_env_select": attr.string_dict(
            doc = "Additional environment variables to set on a crate's `cargo_build_script::env` attribute. Key should be the platform triplet. Value should be a JSON encoded dictionary mapping variable names to values, for example `{\"FOO\": \"bar\"}`.",
        ),
        # "build_script_link_deps": _relative_label_list(
        #     doc = "A list of labels to add to a crate's `cargo_build_script::link_deps` attribute.",
        # ),
        # "build_script_rundir": attr.string(
        #     doc = "An override for the build script's rundir attribute.",
        # ),
        # "build_script_rustc_env": attr.string_dict(
        #     doc = "Additional environment variables to set on a crate's `cargo_build_script::env` attribute.",
        # ),
        "build_script_toolchains": attr.label_list(
            doc = "A list of labels to set on a crates's `cargo_build_script::toolchains` attribute.",
        ),
        "build_script_tags": attr.string_list(
            doc = "A list of tags to add to a crate's `cargo_build_script` target.",
        ),
        "build_script_tools": _relative_label_list(
            doc = "A list of labels to add to a crate's `cargo_build_script::tools` attribute.",
        ),
        "build_script_tools_select": attr.string_list_dict(
            doc = "A list of labels to add to a crate's `cargo_build_script::tools` attribute. Keys should be the platform triplet. Value should be a list of labels.",
        ),
        # "compile_data": _relative_label_list(
        # doc = "A list of labels to add to a crate's `rust_library::compile_data` attribute.",
        # ),
        # "compile_data_glob": attr.string_list(
        # doc = "A list of glob patterns to add to a crate's `rust_library::compile_data` attribute.",
        # ),
        # "compile_data_glob_excludes": attr.string_list(
        # doc = "A list of glob patterns to be excllued from a crate's `rust_library::compile_data` attribute.",
        # ),
        "crate_features": attr.string_list(
            doc = "A list of strings to add to a crate's `rust_library::crate_features` attribute.",
        ),
        "crate_features_select": attr.string_list_dict(
            doc = "A list of strings to add to a crate's `rust_library::crate_features` attribute. Keys should be the platform triplet. Value should be a list of features.",
        ),
        "data": _relative_label_list(
            doc = "A list of labels to add to a crate's `rust_library::data` attribute.",
        ),
        # "data_glob": attr.string_list(
        #     doc = "A list of glob patterns to add to a crate's `rust_library::data` attribute.",
        # ),
        "deps": _relative_label_list(
            doc = "A list of labels to add to a crate's `rust_library::deps` attribute.",
        ),
        "tags": attr.string_list(
            doc = "A list of tags to add to a crate's generated targets.",
        ),
        # "disable_pipelining": attr.bool(
        #     doc = "If True, disables pipelining for library targets for this crate.",
        # ),
        # "extra_aliased_targets": attr.string_dict(
        #     doc = "A list of targets to add to the generated aliases in the root crate_universe repository.",
        # ),
        # "gen_all_binaries": attr.bool(
        #     doc = "If true, generates `rust_binary` targets for all of the crates bins",
        # ),
        "gen_binaries": attr.string_list(
            doc = "As a list, the subset of the crate's bins that should get `rust_binary` targets produced.",
        ),
        "gen_build_script": attr.string(
            doc = "An authoritative flag to determine whether or not to produce `cargo_build_script` targets for the current crate. Supported values are 'on', 'off', and 'auto'.",
            values = ["auto", "on", "off"],
            default = "auto",
        ),
        # "override_target_bin": attr.label(
        #     doc = "An optional alternate target to use when something depends on this crate to allow the parent repo to provide its own version of this dependency.",
        # ),
        # "override_target_build_script": attr.label(
        #     doc = "An optional alternate target to use when something depends on this crate to allow the parent repo to provide its own version of this dependency.",
        # ),
        # "override_target_lib": attr.label(
        #     doc = "An optional alternate target to use when something depends on this crate to allow the parent repo to provide its own version of this dependency.",
        # ),
        # "override_target_proc_macro": attr.label(
        #     doc = "An optional alternate target to use when something depends on this crate to allow the parent repo to provide its own version of this dependency.",
        # ),
        "patch_args": attr.string_list(
            doc = "The `patch_args` attribute of a Bazel repository rule. See [http_archive.patch_args](https://docs.bazel.build/versions/main/repo/http.html#http_archive-patch_args)",
        ),
        "patch_tool": attr.string(
            doc = "The `patch_tool` attribute of a Bazel repository rule. See [http_archive.patch_tool](https://docs.bazel.build/versions/main/repo/http.html#http_archive-patch_tool)",
        ),
        "patches": attr.label_list(
            doc = "The `patches` attribute of a Bazel repository rule. See [http_archive.patches](https://docs.bazel.build/versions/main/repo/http.html#http_archive-patches)",
        ),
        # "rustc_env": attr.string_dict(
        #     doc = "Additional variables to set on a crate's `rust_library::rustc_env` attribute.",
        # ),
        # "rustc_env_files": _relative_label_list(
        #     doc = "A list of labels to set on a crate's `rust_library::rustc_env_files` attribute.",
        # ),
        "rustc_flags": attr.string_list(
            doc = "A list of strings to set on a crate's `rust_library::rustc_flags` attribute.",
        ),
        "rustc_flags_select": attr.string_list_dict(
            doc = "A list of strings to set on a crate's `rust_library::rustc_flags` attribute. Keys should be the platform triplet. Value should be a list of flags.",
        ),
        # "shallow_since": attr.string(
        #     doc = "An optional timestamp used for crates originating from a git repository instead of a crate registry. This flag optimizes fetching the source code.",
        # ),
        "strip_prefix": attr.string(),
        "workspace_cargo_toml": attr.string(
            doc = "For crates from git, the ruleset assumes the (workspace) Cargo.toml is in the repo root. This attribute overrides the assumption.",
            default = "Cargo.toml",
        ),
    },
)

crate = module_extension(
    implementation = _crate_impl,
    tag_classes = {
        "annotation": _annotation,
        "from_cargo": _from_cargo,
    },
)

def _hub_repo_impl(rctx):
    for path, contents in rctx.attr.contents.items():
        rctx.file(path, contents)
    rctx.file("REPO.bazel", "")

_hub_repo = repository_rule(
    implementation = _hub_repo_impl,
    attrs = {
        "contents": attr.string_dict(
            doc = "A mapping of file names to text they should contain.",
            mandatory = True,
        ),
    },
)
